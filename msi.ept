open Iostream

(* ---------------- Header ----------------- *)

(* Sim param *)
const cache_size : int = 3
const nbNode : int = 2

(* Common struct *)
type instr_what = R | W | Nop | Sync | WFlush
type cache_state = M | S | I

type instr_type = {
	what : instr_what ;
	arg : int ;
	v : int
}

const default_instr : instr_type = { what = Nop ; arg = -1 ; v = -1 }

type request_type = {
	t : instr_what ;
	a : int
}

type bus_cache_type = {
	(* M state management *)
	want_ownership : bool ; (* out : you want to go in M state | in : someone wants *)
	who_owns : int ; (* in : who is elected *)

	(* M state info *)
	is_any_M : bool ; (* out : state = M | in : is any M cache block *)
	
	(* Read flag *)
	want_read : bool ; (* out : if try to read and isAnyM | in : is any want_read flag *)

	(* Backing store interaction *)
	want_write : bool ; (* out : you want to write *)
	value : int ; (* out : value to write | in : value in mem *)
	who_writed : int (* in : who writed the last time *)
}

const default_bus_cache : bus_cache_type = {
	want_ownership = false ;
	who_owns = -1 ;
	
	is_any_M = false ;

	want_read = false ;

	want_write = false ;
	value = 0 ;
	who_writed = -1
}

type bus_type = {
	sync : bool ;
	cache_bus : bus_cache_type^cache_size
}

type cache_block_output = {
	(* for processor answer *)
	request_out : int ;
	request_done : bool ;
	(* cache global info *)
	cache_st : cache_state ;
	(* bus out *)
	bus_out : bus_cache_type
}

(* Utils *)
fun stringOfInstr (i : instr_what) returns (o : string)
let
	switch i
		| R do o = "Read" ;
		| W do o = "Write" ;
		| Nop do o = "Nop" ;
		| Sync do o = "Sync" ;
		| WFlush do o = "WFlush"
	end
tel

fun stringOfState (s : cache_state) returns (o : string)
let
	switch s
		| M do o = "M" ;
		| S do o = "S" ;
		| I do o = "I"
	end
tel

(* ----------------- Cache ----------------- *)
node cache_block (
	node_id : int ;
	(* processor in *)
	request : request_type ;
	(* bus in *)
	bus_in : bus_cache_type
) returns (
	(* exported out *)
	out : cache_block_output
) var
	ok : bool ;
	bus_o : bus_cache_type ;
	last data : int = -1 ;
	last st : cache_state = I ;
let
	automaton
		(* Waiting states *)
		state W_I do
			ok = false ;
			bus_o = default_bus_cache
			unless request.t = R then A_I_Want_Read
				| request.t = W then Want_M
		state W_S do
			ok = (request.t = R) ; (* Can read *)
			bus_o = default_bus_cache
			unless bus_in.want_ownership then A_S_Invalidate 
				| request.t = W then Want_M
		state W_M do
			ok = (request.t = R or request.t = W) ; (* Can read or write *)
			bus_o = default_bus_cache
			unless bus_in.want_ownership & bus_in.who_owns <> node_id then Flush_M_to_I
				| bus_in.want_read then Flush_M_to_S
		(* go in M state *)
		state Want_M do
			ok = false ; (* not completed *)
			bus_o = { default_bus_cache with .want_ownership = true } ; (* signal we want the block *)
			unless bus_in.who_owns = node_id then Cached_In_M	
		state Cached_In_M do
			ok = true ; bus_o = default_bus_cache ;
			data = request.a ; (* Cache the write *)
			st = M
			until true then W_M
		(* Flush M state *)
		state Flush_M_to_S do
			ok = false ;
			bus_o = { ({ default_bus_cache with .want_write = true }) with .value = data }
			unless bus_in.who_writed = node_id then Flush_success_M_to_S
		state Flush_success_M_to_S do
			ok = true ; bus_o = default_bus_cache ;
			st = S
			until true then W_S
		state Flush_M_to_I do
			ok = false ;
			bus_o = { ({ default_bus_cache with .want_write = true }) with .value = data }
			unless bus_in.who_writed = node_id then Flush_success_M_to_I
		state Flush_success_M_to_I do
			ok = true ; bus_o = default_bus_cache ;
			st = I
			until true then W_I
		(* Action states for I state *)
		state A_I_Want_Read do
			ok = false ;
			bus_o = { default_bus_cache with .want_read = true } ;
			until not bus_in.is_any_M then A_I_Can_Read
		state A_I_Can_Read do
			ok = true ; bus_o = default_bus_cache ;
			data = bus_in.value ;
			st = S
			until true then W_S
		(* Action states for S state *)
		state A_S_Invalidate do
			ok = false ; bus_o = default_bus_cache ;
			st = I
			until true then W_I
	end ;
	(* Forward real data *)
	out = {
		(* proc *)
		request_out = data ;
		request_done = ok ;
		(* info *)
		cache_st = st ;
		(* bus *)
		bus_out = { bus_o with .is_any_M = (st = M) }
	}
tel

(* ------------ Prog scheduler -------------- *)

(* Try to execute next instruction only if complete is true *)
node prog_scheduler <<
	len : int ;
	(* Data *)
	instrs : instr_type^len
>> (
	complete : bool
) returns (
	instr : instr_type ;
	counter : int
) var
	running : bool ;
let
	(* Pc *)
	running = counter < len ;
	counter = 0 -> pre counter + if complete & pre running then 1 else 0 ;
	(* Arg fetch *)
	instr = if running then instrs[> counter <] else default_instr
tel


(* ----------------- Nodes ----------------- *)

fun make_request (id : int ; instr : instr_type) returns (request : request_type)
let request = {
	t = if instr.what = WFlush or instr.arg = id then instr.what else Nop ;
	a = instr.v
} tel

unsafe node cache_block_helper (
	instr : instr_type ;
	buses_in : bus_cache_type^cache_size ;
	(* Infos *)
	node_id : int ;
	id : int
) returns (
	cache_out : cache_block_output
)
let
	cache_out = cache_block (node_id, make_request (id, instr), buses_in[> id <]) ;
	(* Print *)
	() = printf ("[%d] cache [%d]: (%s,%b) v=%d\n",
		node_id, id,
		stringOfState (cache_out.cache_st), cache_out.request_done, cache_out.request_out
	)
tel

fun cache_op_finished (cur_i : instr_type ; out : cache_block_output ; id : int ; acc : bool)
returns (o : bool)
let o = acc or (cur_i.arg = id & out.request_done) tel

fun cache_ops_finished << size : int >> (outs : cache_block_output^size ; cur_i : instr_type)
returns (o : bool)
let o = foldi << size >> cache_op_finished <(cur_i)> (outs, false) tel

fun isWaitingSync (cur_i : instr_type) returns (o : bool)
let o = cur_i.what = Sync tel

fun getCacheBusOut (i : cache_block_output) returns (o : bus_cache_type)
let o = i.bus_out tel

(* Template node *)
unsafe node node_template <<
	node_id : int ;
	nb_instr : int ;
	program : instr_type^nb_instr
>> (
	(* Bus input *)
	bus_in : bus_type
) returns (
	(* Only output data that is exported (bus data) *)
	bus_out : bus_type
) var
	(* internals *)
	canTick : bool ; (* can we go to next instr *)
	current_instr : instr_type ;
	d_pc : int ; (* info : program counter *)
	(* storing cache states *)
	caches : cache_block_output^cache_size ;
let
	(* Pc *)
	canTick = false -> (
		cache_ops_finished << cache_size >> (pre caches, pre current_instr) or
		((pre bus_out.sync) & bus_in.sync)
	) ;
	(* Pc + program *)
	(current_instr, d_pc) = prog_scheduler << nb_instr, program >> (canTick) ;
	(* Cache *)
	caches = mapi << cache_size >> cache_block_helper <(
		current_instr,
		bus_in.cache_bus,
		node_id
	)> () ;
	(* Outputs *)
	bus_out = {
		sync = isWaitingSync (current_instr) ;
		cache_bus = map << cache_size >> getCacheBusOut (caches)
	} ;
	(* Debug : printfs *)
	() = printf ("[%d] proc: [%d]= %s (%d, %d)\n", node_id, d_pc,
		stringOfInstr (current_instr.what), current_instr.arg, current_instr.v) ;
tel

(* Node 0, instr *)
const n0_nb_i : int = 5
const n0_prog : instr_type^n0_nb_i = [
	{ what = W ; arg = 0 ; v = 42 },
	{ what = Sync ; arg = -1 ; v = -1 },
	{ what = R ; arg = 1 ; v = -1 },
	{ what = W ; arg = 1 ; v = 53 },
	{ what = Nop ; arg = -1 ; v = -1 }
]

(* Node 1, instr *)
const n1_nb_i : int = 7
const n1_prog : instr_type^n1_nb_i = [
	{ what = W ; arg = 1 ; v = 542 },
	{ what = R ; arg = 1 ; v = -1 },
	{ what = Sync ; arg = -1 ; v = -1 },
	{ what = R ; arg = 0 ; v = -1 },
	{ what = W ; arg = 0 ; v = 553 },
	{ what = R ; arg = 0 ; v = -1 },
	{ what = R ; arg = 1 ; v = -1 }
]

(* ------------------ Mem ------------------- *)

node memoryBlock (write : bool ; value : int) returns (o : int ; written : bool)
let
	o = 0 -> if write then value else pre o ;
	written = write
tel

node memoryBlockHelper (request : bus_cache_type) returns (ans : bus_cache_type)
var
	value : int ; written : bool ;
	tmp : bus_cache_type ;
let
	(value, written) = memoryBlock (request.want_write, request.value) ;
	tmp = { request with .value = value } ;
	ans = { tmp with .who_writed = if written then request.who_writed else -1 }
tel

(* ------------------ Bus ------------------- *)
fun isSyncedHelper (b : bus_type ; acc : bool) returns (o : bool)
let o = acc & b.sync tel

fun processCacheBus (node_id : int ; t_b : bus_cache_type ; acc : bus_cache_type)
returns (o : bus_cache_type)
let
	o = {
		(* Deciding which is the M state node (last) *)
		want_ownership = acc.want_ownership or t_b.want_ownership ;
		who_owns = if t_b.want_ownership then node_id else acc.who_owns ;

		is_any_M = t_b.is_any_M or acc.is_any_M ;
		
		want_read = t_b.want_read or acc.want_read ;
		
		(* Prepare inputs for main memory : accept last writer *)
		want_write = t_b.want_write or acc.want_write ;
		value = if t_b.want_write then t_b.value else acc.value ;
		who_writed = if t_b.want_write then node_id else acc.who_writed
	}
tel

fun cacheBusHelper (bus : bus_type ; node_id : int ; acc : bus_cache_type^cache_size)
returns (o : bus_cache_type^cache_size)
let o = map << cache_size >> processCacheBus <(node_id)> (bus.cache_bus, acc) tel

unsafe fun printBusState (bus : bus_cache_type ; id : int) returns ()
let
	() = printf ("bus [%d] : own=(%b,%d) | v=%d | anyM=%b | wantR=%b\n", id,
		bus.want_ownership,
		bus.who_owns,
		bus.value,
		bus.is_any_M, bus.want_read
	)
tel

(* ----------------- Main ------------------ *)
unsafe node main () returns (sep : int^5)
var
	step : int ;	

	bus_outs : bus_type^nbNode ;
	bus_gathered : bus_type ;
	bus_in : bus_type ;
let
	(* Buses *)
	bus_gathered = {
		(* Sync instr bus *)
		sync = false fby fold << nbNode >> isSyncedHelper (bus_outs, true) ;
		(* Cache management bus *)
		cache_bus = (default_bus_cache^cache_size) fby 
			(foldi << nbNode >> cacheBusHelper (bus_outs, default_bus_cache^cache_size))
	} ;
	(* Main mem *)
	bus_in = {
		sync = bus_gathered.sync ;
		cache_bus = map << cache_size >> memoryBlockHelper (bus_gathered.cache_bus) 
	} ;	
	(* Nodes *)
	bus_outs = [
		node_template << 0, n0_nb_i, n0_prog >> (bus_in),
		node_template << 1, n1_nb_i, n1_prog >> (bus_in)
	] ;
	(* Print *)
	() = printf ("bus : sync=%b\n", bus_in.sync) ;
	() = mapi << cache_size >> printBusState (bus_in.cache_bus) ;
	(* Sep *)
	step = 0 fby step + 1 ;
	sep = [step] @ 888888888^4
tel

